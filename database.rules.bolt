hasRole(role) { auth != null && auth.token[role] == true }
isCoordinator() { hasRole('coordinator') }

type CurrentTimestamp extends Number {
  validate() { this == now }
}

type InitialTimestamp extends CurrentTimestamp {
  validate() { prior(this) == null }
}

type ReadOnlyCoordinator {
  read() { isCoordinator() }
}

type WriteOnlyCoordinator {
  write() { isCoordinator() }
}

type WriteOnlyCoordinatorField<T> extends T {
  write() { isCoordinator() }
}

type OnlyNew {
  validate() { prior(this) == null }
}

type EditedFileFieldsStatus {
  validate() {
    this.test(
      /"Spare" | "Given" | "Submitted" | "Revise" | "Done"/
    )
  }
}

type EditedFileFields {
  status: WriteOnlyCoordinatorField<EditedFileFieldsStatus> | Null,
  assignee: WriteOnlyCoordinator | Null,
  followUp: WriteOnlyCoordinatorField<String> | Null
  feedback: WriteOnlyCoordinatorField<String> | Null
}

type EditedFile {
  trackEditing: EditedFileFields | Null,
  soundEngineering: EditedFileFields | Null

  read() { hasRole("coordinator") }
}

path /donations {
  path /cash is ReadOnlyCoordinator {
    path /{push_id} {
      create() {
        return root.donations.tokens[this.token].valid == true
      }
    }
  }
}

path /users is ReadOnlyCoordinator {
  index() {[
    "emailAddress",
    "roles/CR",
    "roles/SQR",
    "roles/SE",
    "uploadCode"
  ]}
  
  path /{userId} {
    path /timestamp is CurrentTimestamp;
    path /isAvailableOnWhatsApp is Boolean;
    path /notes is WriteOnlyCoordinatorField<String>;
    path /emailAddress {
      create() { true }
    }
    path /languages {
      /{langName} is Boolean;
    }
    path /status is WriteOnlyCoordinatorField<String>;
    path /roles is WriteOnlyCoordinator {
      /{roleId} is Boolean;
    }
  }
}

type OriginalFileName extends String {
  validate() { this.test(/^\w+-\d+/) }
}

type Assignee {
  emailAddress: String,
  name: String,
}

type Allotment<Status> {
  assignee: Assignee,
  status: Status,
  notes: String | Null,
  timestampGiven: InitialTimestamp,
  timestampDone: CurrentTimestamp | Null,
  token: String | Null,

  validate() { this.status != 'Done' || this.timestampGiven != null }
}

type ReportingAllotmentStatus extends String {
  validate() { this.test(/^Spare|Given|WIP|Done$/) }
}

type ReportingAllotment extends Allotment<ReportingAllotmentStatus>;

path /allotments {
  path /SQR is Map<OriginalFileName, ReportingAllotment> {
    index() { ["status", "assignee/emailAddress", "token"] }
    read() { query.orderByChild == 'token' && query.equalTo == this.token }
  }
}

path /submissions {
  path /soundQualityReporting {
    path /{list}/{fileName}/{token} {
      read() { true }
      write() {
        return (
          this != null
          &&
          root
            .original[list][fileName]
            .soundQualityReporting
            .token == token
          &&
          root.
            original[list][fileName]
            .soundQualityReporting
            .status != "Done"
        )
      }
      path /complete is OnlyNew;
      path /created is OnlyNew;
    }
  }
}

path /edited/{list}/{fileName} is EditedFile;
