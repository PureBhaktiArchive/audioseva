function isSignedIn() { auth != null }
function hasRole(phase, role) { isSignedIn() && auth.token.roles[phase][role] == true }

type CurrentTimestamp extends Number {
  validate() { this == now }
}

type InitialTimestamp extends Number {
  validate() { this == (prior(this) == null ? now : prior(this)) }
}

path /email/notifications {
    index() { ["sentTimestamp", "template"] }
}

path /donations {
  path /cash {
    path /{push_id} {
      create() {
        return this.parent().parent().tokens[this.token].valid == true
      }
    }
  }
}

// Original file name, for example BR-035A.
type OriginalFileName extends String {
  validate() { this.test(/^\w+-\d+/) }
}

// Task ID, for example ML2-135-3.
type TaskID extends String {
  validate() { this.test(/^\w+-\d+-\d{1,2}$/) }
}

// Status of the allotment.
// Can be either one of the predefined values or virtually any string.
type Status extends String {
  validate() { this.test(/^Spare|Given|WIP|Done|\w+(\s\w+)*]+$/) }
}

type Person {
  uid: String | Null;
  emailAddress: String | Null,
  name: String,

  validate() { this.uid != null || this.emailAddress != null }
}

type Allotment {
  status: Status,
  assignee: Person | Null,
  notes: String | Null,
  timestampGiven: InitialTimestamp | Null,
  timestampDone: InitialTimestamp | Null,

  validate() {
    (this.status != 'Done' || this.timestampDone != null) &&
    (this.status == 'Spare' || (this.timestampGiven != null && this.assignee != null))
  }
}

type ReportingAllotment extends Allotment {
  token: String | Null,

  validate() { this.status == 'Spare' || this.token != null }
}

type Submission {
  author: Person | Null,
  created: Number | Null,
  changed: CurrentTimestamp,
  completed: Number,
  processed: Number | Null,
  comments: String | Null,

  validate() {
    // These fields are not supposed to be changed from the frontend
    this.processed == prior(this.processed) &&
    this.completed == prior(this.completed) &&
    // The same submission should not be updated twice at the same time
    this.changed > prior(this.changed) &&
    this.changed >= this.completed &&
    this.completed >= this.created &&
    true
  }
}

// Audio timing interval, in seconds
type TimingInterval<T> {
  beginning: T,
  ending: T,
}

type AudioAnnotation {
  entireFile: Boolean | Null,
  beginning: String | Null,
  ending: String | Null,
  type: String,
  description: String | Null,

  validate() { this.entireFile != null || (this.beginning != null && this.ending != null) }
}

type SQRSubmission extends Submission {
  duration: TimingInterval<String> | Null,
  soundQualityRating: String,
  soundIssues: AudioAnnotation[],
  unwantedParts: AudioAnnotation[],
}

function tokenIsValid(token, fileName, phase) {
  return phase.allotments[fileName].token == token;
}

path /SQR {
  path /allotments is Map<OriginalFileName, ReportingAllotment> {
    index() { ["status", "assignee/emailAddress", "token"] }
    read() { query.orderByChild == "token" }
  }

  path /submissions {

    // Draft submissions, writable by anyone who knows the token.
    path /drafts/{fileName}/{token} {
      read() { true }
      write() { true }
    }

    // Completed submissions, writable only if the token is valid.
    // This path triggers submission processing.
    path /completed/{fileName}/{token} is SQRSubmission {
      read() { true }
      create() { tokenIsValid(token, fileName, root.SQR) }
      update() {
        tokenIsValid(token, fileName, root.SQR)
        &&
        // Prevent updating is the previous version of the submission was not processed yet
        prior(this.processed) > prior(this.changed)
        &&
        (hasRole('SQR', 'checker') || hasRole('SQR', 'coordinator') || root.SQR.allotments[fileName].status != 'Done')
      }
    }

    path /migrated/{fileName}/{token} is SQRSubmission {
      read() { true }
    }
    
    // Cold storage for the submission, writable only by the function.
    path /final {
      index() { ["author/emailAddress"] }
      path /{fileName} is SQRSubmission;
    }
  }
}

type AudioChunk extends TimingInterval<Number> {
  fileName: String,
  unwantedParts: String,
}

type FileResolution {
  author: Person,
  timestamp: InitialTimestamp,
  isApproved: Boolean,
  feedback: String,

  create() {
    hasRole('TE', 'checker')
    && this.parent().parent().parent().status == 'WIP'
  }
}

type FileVersion {
  timestamp: InitialTimestamp,
  uploadPath: String, // Path in the `uploads` bucket
  resolution: FileResolution | Null,
}

type TrackEditingTask extends Allotment {
  isRestored: Boolean,
  chunks: AudioChunk[],
  versions: FileVersion[],
  timestampImported: InitialTimestamp | Null,

  read() { this.assignee.emailAddress == auth.token.email }
}

path /TE {
  path /tasks is Map<TaskID, TrackEditingTask> {
    index() { ["status", "assignee/emailAddress"] }
    read() {
      isSignedIn() && (
        query.orderByChild == 'assignee/emailAddress' && query.equalTo == auth.token.email
        || hasRole('TE', 'checker')
        || hasRole('TE', 'coordinator')
      )
    }
  }
}

path /quotes {
    read() { true }
    path /{push_id}/text is String {}
}

path /users/{user_id} {
  path /refreshTime {
    read() {
      user_id == auth.uid
    }
  }
}
