function hasRole(role) { auth != null && auth.token[role] == true }
function isCoordinator() { hasRole('coordinator') }

type CurrentTimestamp extends Number {
  validate() { this == now }
}

type InitialTimestamp extends Number {
  validate() { this == (prior(this) == null ? now : prior(this)) }
}

type ReadOnlyCoordinator {
  read() { isCoordinator() }
}

type WriteOnlyCoordinator {
  write() { isCoordinator() }
}

type WriteOnlyCoordinatorField<T> extends T {
  write() { isCoordinator() }
}

type OnlyNew {
  validate() { prior(this) == null }
}

type EditedFileFieldsStatus {
  validate() {
    this.test(
      /"Spare" | "Given" | "Submitted" | "Revise" | "Done"/
    )
  }
}

type EditedFileFields {
  status: WriteOnlyCoordinatorField<EditedFileFieldsStatus> | Null,
  assignee: WriteOnlyCoordinator | Null,
  followUp: WriteOnlyCoordinatorField<String> | Null
  feedback: WriteOnlyCoordinatorField<String> | Null
}

type EditedFile {
  trackEditing: EditedFileFields | Null,
  soundEngineering: EditedFileFields | Null

  read() { hasRole("coordinator") }
}

path /donations {
  path /cash is ReadOnlyCoordinator {
    path /{push_id} {
      create() {
        return root.donations.tokens[this.token].valid == true
      }
    }
  }
}

path /users is ReadOnlyCoordinator {
  index() {[
    "emailAddress",
    "roles/CR",
    "roles/SQR",
    "roles/SE",
    "uploadCode"
  ]}
  
  path /{userId} {
    path /timestamp is CurrentTimestamp;
    path /isAvailableOnWhatsApp is Boolean;
    path /notes is WriteOnlyCoordinatorField<String>;
    path /emailAddress {
      create() { true }
    }
    path /languages {
      /{langName} is Boolean;
    }
    path /status is WriteOnlyCoordinatorField<String>;
    path /roles is WriteOnlyCoordinator {
      /{roleId} is Boolean;
    }
  }
}

type OriginalFileName extends String {
  validate() { this.test(/^\w+-\d+/) }
}

// Status of the allotment.
// Can be either one of the predefined values or virtually any string.
type Status extends String {
  validate() { this.test(/^Spare|Given|WIP|Done|\w+(\s\w+)*]+$/) }
}

type Assignee {
  emailAddress: String,
  name: String,
}

type Allotment {
  assignee: Assignee,
  status: Status,
  notes: String | Null,
  timestampGiven: InitialTimestamp | Null,
  timestampDone: InitialTimestamp | Null,
  token: String | Null,

  validate() { this.status != 'Done' || this.timestampGiven != null }
}

type Submission {
  created: InitialTimestamp,
  changed: CurrentTimestamp,
  completed: InitialTimestamp | Null,
  comments: String | Null,
}

type SQRSubmission extends Submission {
  duration: Object | Null,
  soundQualityRating: String | Null,
  soundIssues: Any | Null,
  unwantedParts: Any | Null,
}

function tokenIsValid(token, fileName, phase) {
  return phase.allotments[fileName].token == token;
}

path /SQR {
  path /allotments is Map<OriginalFileName, Allotment> {
    index() { ["status", "assignee/emailAddress", "token"] }
  }

  path /submissions {

    // Draft submissions, writable by anyone who knows the token.
    path /drafts/{fileName}/{token} is SQRSubmission {
      read() { true }
      write() { true }
    }

    // Completed submissions, writable only if the token is valid.
    // This path triggers submission processing.
    path /completed/{fileName}/{token} is SQRSubmission {
      read() { true }
      create() { tokenIsValid(this.token, fileName, root.SQR) }
      update() {
        tokenIsValid(this.token, fileName, root.SQR)
        &&
        (isCoordinator() || root.SQR.allotments[fileName].status == 'WIP')
      }
    }

    // Cold storage for the submission, writable only by the function.
    path /final/{fileName} is SQRSubmission;
  }
}

path /edited/{list}/{fileName} is EditedFile;
